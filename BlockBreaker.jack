/*
 * Implements the block breaker game
 * This simple game allows the user to move a platform
 * on the bottom of the screen, while a ball is bouncing
 * around the screnn. The user controls the platform as follows:
 * The left and right arrow keys are used to move the platform
 * The 's' key is used to start the game
 * And if all the balls are removed, the game is over
 * The balls can also be removed by hitting the bottom of the screen
 * avoiding your platform
*/

class BlockBreaker{

    field Breaker breaker; // the breaker of this game
    field int direction; // the breaker's current direction
                         // 0 = none, 1 = left, 2 = right

    field Ball ball; // the ball of this game

    field int points;
    field String pointsS;

    field int remainingBlocks;
    field int numBlocks; // number of blocks to be created
    
    field Array currentLevel;

    // ------------- Memory methods --------------

    /** Constructs a new block breaker game. */
    constructor BlockBreaker new(){
        let pointsS = "Points: ";
        do initGame();

        return this;
    }

    /** Disposes this game. */
    method void dispose() {
        do breaker.dispose();
        do ball.dispose();
        do Memory.deAlloc(this);
        return;
    }

    // ------------ Visual Methods --------------

    method void initGame(){

        let points = 0;

        do Screen.setColor(true);
        do Screen.drawRectangle(0, 0, 511, 255); // Draws the background
        do Screen.setColor(false); // Sets the color to white (background color)
        do Screen.drawRectangle(10, 10, 501, 255); // Draws the background

        // The initial breaker is located in (255,240), has a length of 30, and is not moving.
        let breaker = Breaker.new(255, 240, 30);
        let direction = 0; // no direction
        // The initial ball is located in (0,0), has a radius of 5, and is not moving.
        let ball = Ball.new(255, 220, 5, 1, -1);

        do initBlocks(1, 6); // Initializes the blocks (row, columns)

        return;

    }

    method void increaseScore(){
        let points = points + 1;
        let remainingBlocks = remainingBlocks - 1;
        do Screen.setColor(true); // Sets the color to white (background color)
        do Output.moveCursor(0, 0);
        do Output.printString(pointsS); // Prints the score
        do Output.printInt(points); // Prints the score
        do Output.printString(" "); // Prints a space to clear the line
        return;
    }

    method void initBlocks(int row, int column) {
        var int i, j, k;
        var int x, y; // start x and y positions

        let numBlocks = row * column; // number of blocks to be created
        let remainingBlocks = numBlocks;
        let currentLevel = Array.new(remainingBlocks); // Creates an array of blocks

        let k = 0; // Index for the current block
        let x = 60; // Starting x position
        let y = 40; // Starting y position

        // Loop through rows and columns to create blocks
        let i = 0;
        while (i < row) {
            let j = 0;
            while (j < column) {
                let currentLevel[k] = Block.new(x + (j * 70), y + (i * 30), this); // Adjust spacing
                let k = k + 1;
                let j = j + 1;
            }
            let i = i + 1;
        }

        return;
    }

    method void enableBlocks(){
        var int i;
        var Block tmpBlock; // a helper block
        let i = 0;
        while(i < numBlocks){
            let tmpBlock = currentLevel[i];
            do tmpBlock.enable(); // Enable the block
            let i = i + 1;
        }
        return;
    }


    /** Moves the breaker in the current direction. */
    method void moveBreaker() {
        if (direction = 1) { do breaker.moveLeft(); }
        if (direction = 2) { do breaker.moveRight(); }
        return;
    }

    // ------------ Game Logic Methods --------------

    method void gameOver(){

        var char input;
        do Screen.setColor(false); // Sets the color to white (background color)
        do Screen.drawRectangle(0, 0, 511, 255); // Draws the background
        do Output.moveCursor(2,28);
		do Output.printString("Game over");
        do Output.moveCursor(10,15);
		do Output.printString("Please press [ENTER] to continue!");
        do Output.moveCursor(11,14);
		do Output.printString("Or you press [ESC] to exit the game!");
        
        let input = Keyboard.keyPressed();
		while(~((input = 128) | (input = 140))){
			let input = Keyboard.keyPressed();
			do Sys.wait(10);
		}
		if(input = 128){
            do initGame(); // Reinicia el juego
			do run(); // Reinicia el juego
		}else{
            do Sys.halt(); // Termina el programa
		}
		return;

    }
    
    /** Runs the game: handles the user's inputs and moves the breaker accordingly */
    method void run() {
        var char key; // the key currently pressed by the user
        var boolean exit;
        var boolean pause;
        var boolean start;
        var boolean collisionResult; // Variable para almacenar el resultado de handleCollision
        var int i; // a helper iterator 
        var Block tmpBlock; // a helper block
        let pause = false;

        while (~exit) {
            // Captura la tecla presionada
            let key = Keyboard.keyPressed();

            // Maneja las teclas de control
            if (key = 81)  { let exit = true; }     // q key
            if (key = 80)  { let pause = true; }   // p key
            if (key = 82)  { let pause = false; }  // r key
            if (key = 130) { let direction = 1; }  // left arrow
            if (key = 132) { let direction = 2; }  // right arrow
            if (key = 0)   { let direction = 0; }  // no key pressed

            // Mueve el breaker según la dirección
            do moveBreaker();

            // Si el juego no está en pausa, mueve la pelota
            if (~pause) {
                let collisionResult = ball.handleCollision(breaker.getX(), breaker.getY(), breaker.getLength());
                do breaker.breakerCollisionCheck(ball); // Verifica la colisión entre la pelota y el breaker
                do ball.move();

                // Comprueba el valor de collisionResult
                if (~collisionResult) {
                    // Si collisionResult es false, la pelota se perdió
                    do gameOver(); // Llama a la función gameOver
                }
                let i = 0;
                while(i < numBlocks){
                    let tmpBlock = currentLevel[i];
                    do tmpBlock.doCollisionCheck(ball);
                    let i = i+1;
                }
                if(remainingBlocks = 0){
                    do enableBlocks(); // Llama a la función gameOver
                }
            }

            // Espera un breve momento para evitar sobrecarga
            do Sys.wait(5);
        }
        return;
    }

}